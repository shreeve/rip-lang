# Root Layout (with error boundary and navigation indicator)

export Layout = component
  errorMsg := null

  ~> localStorage.setItem '__rip_route', @router.path

  onError: (err) ->
    errorMsg = err.message

  render
    .app-layout
      nav.app-nav
        .nav-inner
          a.nav-brand "Rip UI"
          .nav-links
            a.('nav-link', @router.path is '/' and 'active') href: "#", "Home"
            a.('nav-link', @router.path is '/counter' and 'active') href: "#counter", "Counter"
            a.('nav-link', @router.path is '/todos' and 'active') href: "#todos", "Todos"
            a.('nav-link', @router.path is '/about' and 'active') href: "#about", "About"
            if @router.navigating
              span.nav-loading "Loading..."
      main.app-main
        if errorMsg
          .error-banner
            p "Something went wrong: #{errorMsg}"
            button.btn @click: (-> errorMsg = null), "Dismiss"
        #content
