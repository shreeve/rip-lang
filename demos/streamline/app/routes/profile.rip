# Profile page - view and edit user info

export Profile = component
  user := null
  firstName := ''
  lastName := ''
  phone := ''
  loading := true
  saving := false
  saved := false
  error := null

  beforeMount: ->
    try
      res = await fetch('/api/user')
      data = await res.json()
      if data.user
        user = data.user
        firstName = user.firstName or ''
        lastName = user.lastName or ''
        phone = user.phone or ''
      else
        @router.push '/auth'
    catch
      @router.push '/auth'
    loading = false

  save: ->
    return if saving
    saving = true
    saved = false
    error = null
    try
      res = await fetch '/api/user',
        method: 'PATCH'
        headers: { 'Content-Type': 'application/json' }
        body: JSON.stringify { ["firstName"]: firstName, ["lastName"]: lastName, ["phone"]: phone }
      data = await res.json()
      if data.error
        error = data.error
      else
        saved = true
    catch err
      error = err.message or 'Failed to save'
    saving = false

  render
    .('py-12')
      h1.('text-3xl font-semibold') "Profile"

      if loading
        .('flex items-center justify-center py-20')
          p.('text-tertiary') "Loading..."

      if user and not loading
        .('mt-10 max-w-[640px] rounded-xl border border-primary bg-primary p-5 md:p-7 lg:p-9')
          .('grid gap-5 sm:grid-cols-2')
            .
              label.('block text-sm font-medium text-secondary mb-1.5') "First name"
              input.('block w-full rounded-lg border border-primary bg-primary px-3.5 py-2.5 text-sm-plus text-primary shadow-xs transition-colors focus:border-brand focus:ring-1 focus:ring-brand outline-none') type: "text", value: firstName, placeholder: "First name"
            .
              label.('block text-sm font-medium text-secondary mb-1.5') "Last name"
              input.('block w-full rounded-lg border border-primary bg-primary px-3.5 py-2.5 text-sm-plus text-primary shadow-xs transition-colors focus:border-brand focus:ring-1 focus:ring-brand outline-none') type: "text", value: lastName, placeholder: "Last name"
            .
              label.('block text-sm font-medium text-secondary mb-1.5') "Email"
              input.('block w-full rounded-lg border border-primary bg-tertiary px-3.5 py-2.5 text-sm-plus text-tertiary shadow-xs cursor-not-allowed') type: "email", value: user.email, disabled: true
            .
              label.('block text-sm font-medium text-secondary mb-1.5') "Phone"
              input.('block w-full rounded-lg border border-primary bg-primary px-3.5 py-2.5 text-sm-plus text-primary shadow-xs transition-colors focus:border-brand focus:ring-1 focus:ring-brand outline-none') type: "tel", value: phone, placeholder: "Phone number"

          if error
            .('mt-5 rounded-lg border border-red-200 bg-red-50 px-4 py-3 text-sm text-error') error
          if saved
            .('mt-5 rounded-lg border border-green-200 bg-green-50 px-4 py-3 text-sm text-green-700') "Profile updated!"

          button.(
            'mt-7 flex w-full items-center justify-center rounded-lg bg-brand px-4 py-2.5 text-sm-plus',
            'font-semibold text-white transition-colors hover:bg-brand-hover active:bg-brand-active disabled:opacity-50',
          )
            class: .('sm:max-w-[50%] md:mt-8 lg:mt-9')
            disabled: saving
            @click: @save
            if saving then "Saving..." else "Update Profile"
