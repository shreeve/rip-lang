# DetailPage - Reusable health detail page with gradient header and gauges

export DetailPage = component
  @topic := {}
  @history := []
  @ranges := {}
  @acceptable := null

  shouldShow ~=
    return true unless topic.conditional
    history and history[0] and history[0][topic.conditional]?

  tipIcons :=
    utensils: '<svg width="14" height="14" viewBox="0 0 24 24" fill="none" stroke="currentColor" stroke-width="1.5"><path d="M3 2v7c0 1.1.9 2 2 2h4c1.1 0 2-.9 2-2V2"/><line x1="7" y1="2" x2="7" y2="22"/><path d="M21 15V2a5 5 0 00-5 5v6h4"/><line x1="21" y1="12" x2="21" y2="22"/></svg>'
    drop: '<svg width="14" height="14" viewBox="0 0 24 24" fill="none" stroke="currentColor" stroke-width="1.5"><path d="M12 2.69l5.66 5.66a8 8 0 11-11.31 0z"/></svg>'
    walk: '<svg width="14" height="14" viewBox="0 0 24 24" fill="none" stroke="currentColor" stroke-width="1.5"><circle cx="13" cy="4" r="2"/><path d="M7 21l3-4V11l-2-2-3 3"/><path d="M16 21l-2-6-3-1"/></svg>'
    doctor: '<svg width="14" height="14" viewBox="0 0 24 24" fill="none" stroke="currentColor" stroke-width="1.5"><path d="M12 11V3M4.93 7.5l7.07 3.5 7.07-3.5"/><circle cx="12" cy="18" r="3"/><line x1="12" y1="15" x2="12" y2="11"/></svg>'
    smile: '<svg width="14" height="14" viewBox="0 0 24 24" fill="none" stroke="currentColor" stroke-width="1.5"><circle cx="12" cy="12" r="10"/><path d="M8 14s1.5 2 4 2 4-2 4-2"/><line x1="9" y1="9" x2="9.01" y2="9"/><line x1="15" y1="9" x2="15.01" y2="9"/></svg>'

  checkIcon := '<svg width="20" height="20" viewBox="0 0 24 24" fill="currentColor"><path d="M12 2C6.48 2 2 6.48 2 12s4.48 10 10 10 10-4.48 10-10S17.52 2 12 2zm-2 15l-5-5 1.41-1.41L10 14.17l7.59-7.59L19 8l-9 9z"/></svg>'
  warnIcon  := '<svg width="20" height="20" viewBox="0 0 24 24" fill="currentColor"><path d="M12 2C6.48 2 2 6.48 2 12s4.48 10 10 10 10-4.48 10-10S17.52 2 12 2zm1 15h-2v-2h2v2zm0-4h-2V7h2v6z"/></svg>'

  statusOf: (key) ->
    val = history[0]?[key]
    return 'acceptable' unless val?
    acceptable(key, val)

  iconFor: (key) ->
    if @statusOf(key) is 'acceptable' then checkIcon else warnIcon

  render
    if shouldShow
      .('brochure__page page-detail')
        if topic.gradient
          .detail__header style: "background: #{topic.gradient}"
            .detail__header-image style: "background-image: url(#{topic.image}); width: #{topic.imageWidth}px; height: #{topic.imageHeight}px"
            .detail__header-text
              .detail__header-title topic.title
              .detail__header-copy topic.copy

        if topic.isImagePage
          .detail__header style: "background-image: url(#{topic.image}); background-size: cover"

        .detail__content
          for bio in topic.biomarkers
            .detail__item
              if ranges[bio.key]
                Gauge value: (history[0]?[bio.key] or 0), units: ranges[bio.key].units, range: ranges[bio.key].range

                .detail__item-header class: @statusOf(bio.key)
                  .status-icon
                    innerHTML: @iconFor(bio.key)
                  div
                    .name ranges[bio.key].name
                    .desc ranges[bio.key].desc

                .detail__item-info
                  p innerHTML: bio.text
                  if bio.extra
                    p bio.extra

                .detail__maintenance
                  .detail__maintenance-heading "How To Maintain"
                  for tip in bio.tips
                    .detail__maintenance-item
                      .detail__maintenance-icon
                        innerHTML: (tipIcons[tip.icon] or '')
                      div tip.text
