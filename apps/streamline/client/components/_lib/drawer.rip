# Drawer - responsive slide panel (bottom on mobile, right on desktop)
#
# Usage:
#   Drawer open: showDrawer, @close: (=> showDrawer = false)
#     div "Drawer content here"

export Drawer = component
  open := false
  breakpoint := 480

  isRight := false
  closing := false

  onMount: ->
    mq = window.matchMedia("(min-width: #{breakpoint}px)")
    isRight = mq.matches
    handler = (e) -> isRight = e.matches
    mq.addEventListener 'change', handler

  close: ->
    closing = true
    setTimeout ->
      closing = false
      @emit 'close'
    , 200

  onOverlayClick: (e) ->
    @close() if e.target is e.currentTarget

  onKeyDown: (e) ->
    @close() if e.key is 'Escape'

  render
    if open or closing
      # Overlay
      .('fixed inset-0 z-50 transition-opacity duration-200',
        if closing then 'bg-black/0 pointer-events-none' else 'bg-black/12') @click: @onOverlayClick, @keydown: @onKeyDown, tabindex: "-1",

        # Panel
        .('fixed bottom-0 right-0 bg-primary outline-none transition-transform duration-200',
          if isRight then 'top-0 w-[400px] max-w-[85vw]' else 'left-0 overflow-hidden rounded-t-3xl',
          if isRight
            if closing then 'translate-x-full' else 'translate-x-0'
          else
            if closing then 'translate-y-full' else 'translate-y-0')

          # Drag handle (mobile only)
          unless isRight
            .('flex justify-center py-3')
              .('h-1.5 w-10 rounded-full bg-quaternary')

          # Scrollable content
          .('h-full overflow-y-auto') style: "max-height: calc(100dvh - 40px); scrollbar-width: none",
            #content
